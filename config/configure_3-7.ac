AC_PREREQ([2.57])
AC_INIT([gthumb-openexr-viewer], [1.0])
AM_INIT_AUTOMAKE([1.9 foreign])

AC_CONFIG_HEADERS([config.h])
AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_SRCDIR([configure.ac])

if test -z "$enable_maintainer_mode"; then
  enable_maintainer_mode=yes
fi
AM_MAINTAINER_MODE([enable])

m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

AM_PROG_LIBTOOL

m4_pattern_allow([^AM_])
m4_pattern_allow([^AC_])
m4_pattern_allow([^AS_])

AC_ISC_POSIX
AC_PROG_CC
AC_PROG_CXX
AM_PROG_CC_STDC
AM_PROG_CC_C_O
AC_HEADER_STDC
AC_C_BIGENDIAN
AC_PROG_CPP
AC_PROG_CXX

GNOME_COMMON_INIT
GNOME_DOC_INIT
GNOME_MAINTAINER_MODE_DEFINES
GNOME_COMPILE_WARNINGS([maximum])
GTHUMB_EXTENSION_INIT(3.7)
GTHUMB_EXTENSION_RULES

GLIB_REQUIRED=2.16.0
GTK_REQUIRED=3.0
GCONF_REQUIRED=2.6.0
GTHUMB_REQUIRED=3.7

dnl ===========================================================================

CFLAGS=${CFLAGS-:""}
CFLAGS+=" -DG_DISABLE_SINGLE_INCLUDES -DGDK_PIXBUF_DISABLE_SINGLE_INCLUDES -DGTK_DISABLE_SINGLE_INCLUDES"
CFLAGS+=" -DG_DISABLE_DEPRECATED -DGDK_DISABLE_DEPRECATED -DGDK_PIXBUF_DISABLE_DEPRECATED -DGTK_DISABLE_DEPRECATED"

AC_ARG_ENABLE(debug,
	      AS_HELP_STRING([--enable-debug], [enable compilation of debugging messages]),
	      [case "${enableval}" in
	      yes) ENABLE_DEBUG=yes ;;
	      no) ENABLE_DEBUG=no ;;
	      *) AC_MSG_ERROR([bad value ${enableval} for --enable-debug]) ;;
	      esac],
	      [ENABLE_DEBUG=no])
if test x$ENABLE_DEBUG = xyes; then
	AC_DEFINE(DEBUG, 1, [enable compilation of debugging messages])
	CFLAGS+=" -g -O0"
fi

dnl ===========================================================================

PKG_CHECK_MODULES(EXTENSION, [
	glib-2.0 		>= $GLIB_REQUIRED
	gthread-2.0
	gmodule-2.0
	gio-unix-2.0
	gtk+-3.0 		>= $GTK_REQUIRED
	gconf-2.0 		>= $GCONF_REQUIRED
	gthumb-3.7		>= $GTHUMB_REQUIRED
	OpenEXR
	IlmBase
])
AC_SUBST(EXTENSION_LIBS)
AC_SUBST(EXTENSION_CFLAGS)

EXTENSION_DIR=`pkg-config --variable=extensionsdir gthumb-3.7`
AC_SUBST(EXTENSION_DIR)

EXTENSION_LIBTOOL_FLAGS='-module -avoid-version -disable-static'
AC_SUBST(EXTENSION_LIBTOOL_FLAGS)

dnl ===========================================================================

IT_PROG_INTLTOOL([0.35.0])
GETTEXT_PACKAGE=gthumb-openexr-viewer
AC_SUBST([GETTEXT_PACKAGE])
AC_DEFINE_UNQUOTED([GETTEXT_PACKAGE],["$GETTEXT_PACKAGE"],[Gettext package])
AM_GLIB_GNU_GETTEXT

if test "x${prefix}" = "xNONE"; then
	AC_DEFINE_UNQUOTED(LOCALEDIR, "${ac_default_prefix}/${DATADIRNAME}/locale", [Locale directory])
else
	AC_DEFINE_UNQUOTED(LOCALEDIR, "${prefix}/${DATADIRNAME}/locale", [Locale directory])
fi

dnl ===========================================================================

AC_PATH_PROG(GCONFTOOL, gconftool-2, no)
if test x"$GCONFTOOL" = xno; then
	AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi
AM_GCONF_SOURCE_2

dnl ===========================================================================

AC_CONFIG_FILES([
Makefile
src/Makefile
po/Makefile.in
])

AC_OUTPUT

echo "
Configuration:

	Source code location : $srcdir
	Compiler             : $CC
	Prefix               : $prefix
	CFLAGS			     : $CFLAGS
	Debug                : $ENABLE_DEBUG
"
